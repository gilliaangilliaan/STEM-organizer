@echo off
setlocal EnableExtensions
cd /d "%~dp0"

echo.
echo Rename Auto-detect - install dependencies
echo.
echo You need:
echo   - Python 3.10 or 3.11 on PATH
echo   - Internet ^(hear21passt / PaSST weights on first use^)
echo.
echo Installs into shared genre_gender_tagger\venv
echo ^(run Genre ^& Gender install first if that venv is empty^).
echo.
echo Frozen STEM-organizer.exe: use root install-deps.bat beside the .exe
echo instead ^(puts hear21passt into site-packages\^).
echo.
REM Refuse accidental double-click next to a frozen build (wrong target venv).
if /I not "%STEM_INST_BUNDLED%"=="1" if /I not "%STEM_GG_BUNDLED%"=="1" (
    if exist "%~dp0..\STEM-organizer.exe" (
        echo ERROR: STEM-organizer.exe detected in parent folder.
        echo Do NOT run this nested bat for a frozen build.
        echo Run instead:
        echo   %~dp0..\install-deps.bat
        echo ^(installs hear21passt into site-packages next to the .exe^).
        pause
        exit /b 1
    )
)

where python >nul 2>&1
if errorlevel 1 (
    echo ERROR: python not on PATH.
    echo Install 3.10/3.11 - tick "Add to PATH".
    pause
    exit /b 1
)

python -c "import sys; raise SystemExit(0 if sys.version_info[:2] in ((3,10),(3,11)) else 1)"
if errorlevel 1 (
    echo ERROR: need Python 3.10 or 3.11. Got:
    python --version
    echo Tip: py -3.11 "%~f0"
    pause
    exit /b 1
)

echo Python:
python --version
echo.

set "VENV=%~dp0..\genre_gender_tagger\venv"
if not exist "%VENV%\Scripts\python.exe" (
    echo Creating genre_gender_tagger\venv ...
    python -m venv "%VENV%"
    if errorlevel 1 (
        echo ERROR: could not create shared venv.
        pause
        exit /b 1
    )
)

set "PY=%VENV%\Scripts\python.exe"

set "INST_TORCH_CHOICE=%~1"
if "%INST_TORCH_CHOICE%"=="" set "INST_TORCH_CHOICE=%STEM_GG_TORCH%"
if "%INST_TORCH_CHOICE%"=="1" goto cuda_torch
if "%INST_TORCH_CHOICE%"=="2" goto cpu_torch
if "%INST_TORCH_CHOICE%"=="3" goto cuda_new_torch
goto ask_gpu

:ask_gpu
echo PyTorch build ^(shared venv^):
echo   1 = NVIDIA RTX 20/30/40  ^(CUDA 12.4^)
echo   2 = CPU only
echo   3 = NVIDIA RTX 50-series ^(CUDA 12.8^)
choice /C 123 /N /M "Enter 1, 2, or 3: "
if errorlevel 3 goto cuda_new_torch
if errorlevel 2 goto cpu_torch
if errorlevel 1 goto cuda_torch
goto ask_gpu

:cuda_torch
set "TORCH_INDEX=https://download.pytorch.org/whl/cu124"
set "TORCH_LABEL=CUDA 12.4"
goto install_pkgs

:cuda_new_torch
set "TORCH_INDEX=https://download.pytorch.org/whl/cu128"
set "TORCH_LABEL=CUDA 12.8"
goto install_pkgs

:cpu_torch
set "TORCH_INDEX=https://download.pytorch.org/whl/cpu"
set "TORCH_LABEL=CPU"
goto install_pkgs

:install_pkgs
echo.
echo [1/3] audio helpers ...
"%PY%" -m pip install --upgrade pip
if errorlevel 1 goto fail
"%PY%" -m pip install "numpy>=1.24,<2.2" soundfile librosa
if errorlevel 1 goto fail

echo [2/3] hear21passt + timm ...
"%PY%" -m pip install hear21passt --no-deps
if errorlevel 1 goto fail
"%PY%" -m pip install timm --no-deps
if errorlevel 1 goto fail
"%PY%" -m pip install pyyaml huggingface_hub safetensors packaging
if errorlevel 1 goto fail

echo [3/3] torch + torchaudio + torchvision (%TORCH_LABEL%) ...
"%PY%" -m pip install --force-reinstall torch torchaudio torchvision --index-url "%TORCH_INDEX%"
if errorlevel 1 (
    echo WARNING: torch stack install failed - leaving existing packages.
)

"%PY%" -c "import torch, torchaudio, torchvision; print('OK torch', torch.__version__, 'torchaudio', torchaudio.__version__, 'torchvision', torchvision.__version__, 'cuda=', torch.cuda.is_available())"
"%PY%" -c "import hear21passt; print('OK hear21passt')"
if errorlevel 1 goto fail

echo.
echo Done (%TORCH_LABEL%). First Auto-detect run downloads ~330 MB weights.
if /I not "%STEM_INST_BUNDLED%"=="1" if /I not "%STEM_GG_BUNDLED%"=="1" pause
exit /b 0

:fail
echo.
echo ERROR: install failed. Check messages above.
pause
exit /b 1
